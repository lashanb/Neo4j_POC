LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/db_legal_entities.csv' AS row
MERGE (db:DBLegalEntity {dbEntityId: row.dbEntityId})
SET db.name = row.name,
    db.region = row.region,
    db.country = row.country,
    db.lei = row.lei,
    db.description = row.Description;

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/clients.csv' AS row
MERGE (c:ClientCorporate {clientId: row.clientId})
SET c.name = row.name,
    c.industry = row.industry,
    c.country = row.country,
    c.lei = row.lei;

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/individuals.csv' AS row
MERGE (p:Individual {personId: row.personId})
SET p.name = row.name,
    p.role = row.role,
    p.nationality = row.nationality

WITH row, p
MATCH (c:ClientCorporate {clientId: row.clientId})
MERGE (p)-[:UBO_OR_DIRECTOR_OF]->(c);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/applications.csv' AS row
MERGE (a:Application {applicationId: row.ApplicationID})
SET a.name = row.Name,
    a.itOwner = row.ITOwner,
    a.cio = row.CIO,
    a.owningOrgUnit = row.OwningOrgUnit,
    a.processCapability = row.ProcessCapability,
    a.description = row.Description;

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/contracts.csv' AS row
MERGE (k:Contract {contractId: row.contractId})
SET k.contractType = row.contractType,
    k.startDate = date(row.startDate),
    k.endDate = date(row.endDate),
    k.currency = row.currency,
    k.amount = toFloat(row.amount),
    k.status = row.status,
    k.name = row.contractId
	
WITH row, k
MATCH (a:Application {applicationId: row.ApplicationID})
MERGE (a)-[:PUBLISHES]->(k)

WITH row, k
MATCH (db:DBLegalEntity {dbEntityId: row.dbEntityId})
MERGE (db)-[:HAS_CONTRACT]->(k)

WITH row, k
MATCH (c:ClientCorporate {clientId: row.clientId})
MERGE (c)-[:HAS_CONTRACT]->(k);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/collateral.csv' AS row
MERGE (coll:Collateral {contractId: row.contractId})
SET coll.contractType = row.contractType,
    coll.startDate = date(row.startDate),
    coll.endDate = date(row.endDate),
    coll.currency = row.currency,
    coll.amount = toFloat(row.amount),
    coll.status = row.status;

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/contract_collateral.csv' AS row
MATCH (k:Contract {contractId: row.contractId})
MATCH (coll:Collateral {contractId: row.collateralId})
MERGE (k)-[:COLLATERALIZED_BY]->(coll);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/merchants.csv' AS row
MERGE (m:Merchant {merchantId: row.merchantId})
SET m.name = row.name,
    m.industry = row.industry,
    m.country = row.country;

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/transactions.csv' AS row
MERGE (t:Transaction {txnId: row.txnId})
SET t.date = date(row.date),
    t.amount = toFloat(row.amount),
    t.currency = row.currency,
    t.txnType = row.txnType,
    t.name = row.txnId

WITH row, t
MATCH (a:Application {applicationId: row.ApplicationID})
MERGE (a)-[:PUBLISHES]->(t)

WITH row, t
MATCH (k:Contract {contractId: row.contractId})
MERGE (k)-[:MADE]->(t)

WITH row, t
MATCH (m:Merchant {merchantId: row.merchantId})
MERGE (t)-[:TO]->(m);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/jurisdictions.csv' AS row
MERGE (j:Jurisdiction {jurisdictionId: row.jurisdictionId})
SET j.name = row.name,
    j.isoCode = row.isoCode;

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/entity_jurisdiction.csv' AS row
WITH row
WHERE row.entityType = 'DB'
MATCH (db:DBLegalEntity {dbEntityId: row.entityId})
MATCH (j:Jurisdiction {jurisdictionId: row.jurisdictionId})
MERGE (db)-[:REGISTERED_IN]->(j);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/entity_jurisdiction.csv' AS row
WITH row
WHERE row.entityType = 'Client'
MATCH (c:ClientCorporate {clientId: row.entityId})
MATCH (j:Jurisdiction {jurisdictionId: row.jurisdictionId})
MERGE (c)-[:REGISTERED_IN]->(j);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/tariffs.csv' AS row
MERGE (t:Tariff {tariffId: row.tariffId})
SET t.HSCode = row.HSCode,
    t.description = row.description,
    t.ratePct = toFloat(row.ratePct),
    t.name = row.tariffId ;

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/events_enriched.csv' AS row
MERGE (ne:NewsEvent {eventId: row.eventId})
SET ne.headline = row.headline,
    ne.source = row.source,
    ne.date = date(row.date),
    ne.type = row.type,
    ne.industry = row.industry,
    ne.country = row.country,
    ne.name = row.headline

WITH row, ne
WHERE row.clientId IS NOT NULL
MATCH (c:ClientCorporate {clientId: row.clientId})
MERGE (ne)-[:AFFECTS]->(c)

WITH row, ne
WHERE row.dbEntityId IS NOT NULL
MATCH (db:DBLegalEntity {dbEntityId: row.dbEntityId})
MERGE (ne)-[:AFFECTS]->(db);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/tariff_transaction.csv' AS row
MATCH (t:Tariff {tariffId: row.tariffId})
MATCH (txn:Transaction {txnId: row.txnId})
MERGE (t)-[:APPLIES_TO]->(txn);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/applications.csv' AS row
MERGE (a:Application {applicationId: row.ApplicationID})
SET a.name = row.Name,
    a.itOwner = row.ITOwner,
    a.cio = row.CIO,
    a.owningOrgUnit = row.OwningOrgUnit,
    a.processCapability = row.ProcessCapability;

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/DataFeeds.csv' AS row
MERGE (df:DataFeed {dataFeedId: row.DataFeed_ID})
SET df.edm_data_class = row.`EDM Data Class`,
    df.protocolUri = row.`Protocol URI`
WITH row, df
MATCH (a:Application {applicationId: row.Application_ID})
MERGE (a)-[:OWNS_FEED]->(df)
WITH row, df
MATCH (sdp:SDPModel {sdpModelId: row.`SDP Model ID`})
MERGE (df)-[:CONFORMS_TO]->(sdp);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/edm_data_classes.csv' AS row
MERGE (edm:EDMDataClass {edmId: row.id})
SET edm.name = row.name,
    edm.description = row.description;

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/sdp_models.csv' AS row
MERGE (sdp:SDPModel {sdpModelId: row.id})
SET sdp.name = row.name,
    sdp.protocolUri = row.protocolUri
WITH row, sdp
MATCH (edm:EDMDataClass {edmId: row.edmDataClassId})
MERGE (sdp)-[:IMPLEMENTS]->(edm);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/sdp_attributes.csv' AS row
MERGE (attr:SDPAttribute {name: row.name, sdpModelId: row.sdpModelId})
SET attr.definition = row.definition,
    attr.pii = toBoolean(row.pii)
WITH row, attr
MATCH (sdp:SDPModel {sdpModelId: row.sdpModelId})
MERGE (sdp)-[:HAS_ATTRIBUTE]->(attr);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/ControlRegistry.csv' AS row
MERGE (cr:Control {controlId: row.`Control ID`})
SET cr.controlType = row.`Control Type`,
    cr.dataAttribute = row.`Data Attribute`,
    cr.edm_data_class = row.`EDM Data Class`
WITH row, cr
MATCH (a:Application {applicationId: row.`Application ID`})
MERGE (a)-[:OWNS_CONTROL]->(cr)
WITH row, cr
MATCH (df:DataFeed {dataFeedId: row.`Data Feed ID`})
MERGE (df)-[:HAS_CONTROL]->(cr);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/DQSummary.csv' AS row
MERGE (dqs:DQSummary {summaryId: row.`DQ Summary ID`})
SET dqs.passRatio = toFloat(row.`Pass Ratio`)
WITH row, dqs
MATCH (cr:Control {controlId: row.`Control ID`})
MERGE (cr)-[:HAS_SUMMARY]->(dqs)
WITH row, dqs
MATCH (df:DataFeed {dataFeedId: row.`Data Feed ID`})
MERGE (df)-[:HAS_SUMMARY]->(dqs);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/Adjustments.csv' AS row
MERGE (adj:Adjustment {adjustmentId: row.AdjustmentID})
SET adj.adjustmentAmount = toFloat(row.`Adjustment Amount`),
    adj.performedBy = row.Performed_by

// CONTRACT ADJUSTMENT
WITH row, adj
OPTIONAL MATCH (k:Contract {contractId: row.`Contract ID`})
FOREACH (_ IN CASE WHEN k IS NOT NULL THEN [1] ELSE [] END |
    MERGE (adj)-[:ADJUSTS_CONTRACT]->(k)
)

// TRANSACTION ADJUSTMENT
WITH row, adj
OPTIONAL MATCH (t:Transaction {txnId: row.`Transaction ID`})
FOREACH (_ IN CASE WHEN t IS NOT NULL THEN [1] ELSE [] END |
    MERGE (adj)-[:ADJUSTS_TRANSACTION]->(t)
);

LOAD CSV WITH HEADERS FROM 'https://raw.githubusercontent.com/lashanb/Neo4j_POC/main/Sample%20Data/logical_data_flows.csv' AS row
MERGE (ldf:LogicalDataFlow {flowId: row.FlowID})
SET ldf.owner = row.DataFlowOwner
WITH row, ldf
MATCH (fromApp:Application {applicationId: row.FromAppID})
MERGE (fromApp)-[:SENDS_DATA_FLOW]->(ldf)
WITH row, ldf
MATCH (toApp:Application {applicationId: row.ToAppID})
MERGE (ldf)-[:FLOWS_TO]->(toApp)
WITH row, ldf
MATCH (edm:EDMDataClass {edmId: row.EDMDataClassID})
MERGE (ldf)-[:CARRIES_DATA]->(edm);
